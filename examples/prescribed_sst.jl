# # Prescribed sea surface temperature convection
#
# This example simulates moist convection driven by a prescribed sea surface temperature (SST).
# The simulation models the atmospheric response to a horizontally-varying SST pattern,
# a fundamental problem in atmosphere-ocean interaction studies. The setup is representative
# of convection over oceanic fronts or sea surface temperature gradients, where differential
# heating drives organized atmospheric circulations.
#
# The simulation uses bulk aerodynamic formulas to compute surface fluxes of momentum,
# sensible heat, and latent heat based on bulk transfer coefficients. This approach
# parameterizes the complex turbulent exchange processes in the surface layer using
# simple drag law formulations that relate fluxes to the difference between surface
# and near-surface atmospheric properties.
#
# The model uses warm-phase saturation adjustment microphysics with liquid-ice
# potential temperature thermodynamics. Saturation adjustment instantly condenses
# or evaporates water vapor to maintain thermodynamic equilibrium, providing a
# simple yet effective representation of cloud processes in moist convection.

using Breeze
using Oceananigans
using Oceananigans.Units
using Oceananigans.Models: BoundaryConditionOperation
using Printf
using CairoMakie

# ## Grid setup
#
# We use a 2D domain (x-z plane) with periodic horizontal boundaries and a bounded
# vertical domain. The horizontal periodicity allows convective cells to develop
# and interact without artificial boundary effects. The domain extends 20 km
# horizontally to accommodate multiple convective cells, and 10 km vertically
# to capture the full depth of tropospheric convection.
#
# The grid resolution of 128 points in each direction provides approximately
# 156 m horizontal and 78 m vertical resolution, sufficient to resolve the
# energy-containing scales of convective turbulence while remaining computationally
# tractable for this demonstration.

grid = RectilinearGrid(size = (128, 128), halo = (5, 5),
                       x = (-10kilometers, 10kilometers),
                       z = (0, 10kilometers),
                       topology = (Periodic, Flat, Bounded))

# ## Model formulation
#
# We create an AtmosphereModel with warm-phase saturation adjustment microphysics
# and liquid-ice potential temperature thermodynamics. The anelastic formulation
# filters acoustic waves while retaining the essential dynamics of deep convection,
# allowing larger time steps than a fully compressible model.
#
# The reference state defines the background thermodynamic profile against which
# perturbations evolve. We use a base pressure pâ‚€ = 101325 Pa (standard sea level
# pressure) and reference potential temperature Î¸â‚€ = 285 K, representing a
# relatively cool maritime atmosphere.

pâ‚€, Î¸â‚€ = 101325, 285 # Pa, K
constants = ThermodynamicConstants()
reference_state = ReferenceState(grid, constants; base_pressure=pâ‚€, potential_temperature=Î¸â‚€)
formulation = AnelasticFormulation(reference_state, thermodynamics = :LiquidIcePotentialTemperature)

# The microphysics scheme uses saturation adjustment to maintain thermodynamic
# equilibrium. The `WarmPhaseEquilibrium` option considers only liquid water
# and vapor, appropriate for warm convection where ice processes are negligible.

microphysics = SaturationAdjustment(equilibrium = WarmPhaseEquilibrium())

# We use high-order WENO advection schemes to accurately represent the sharp
# gradients that develop in convective flows. WENO (Weighted Essentially
# Non-Oscillatory; [Shu09](@citet)) schemes provide excellent shock-capturing
# properties while maintaining high accuracy in smooth regions.

momentum_advection = WENO(order=9)
scalar_advection = WENO(order=5)

# ## Surface flux parameterization
#
# The surface fluxes are computed using bulk aerodynamic formulas, which relate
# the turbulent fluxes to the difference between surface and atmospheric properties
# multiplied by a transfer coefficient and wind speed. This approach parameterizes
# the complex turbulent exchange processes in the atmospheric surface layer.
#
# The bulk transfer coefficients are:
# - Cá´° (drag coefficient): relates surface momentum flux to wind speed
# - Cáµ€ (sensible heat transfer coefficient): relates sensible heat flux to temperature difference
# - Cáµ› (vapor transfer coefficient): relates latent heat flux to humidity difference
#
# The sea surface temperature varies as a step function across the domain center,
# creating a sharp SST front. This idealized pattern drives a strong circulation
# with rising motion over the warm side and sinking motion over the cold side.

using Breeze.Thermodynamics:
    saturation_specific_humidity,
    base_density,
    PlanarLiquidSurface

Î”T = 2 # K
Ïâ‚€ = base_density(pâ‚€, Î¸â‚€, constants)
surface = PlanarLiquidSurface()

# Sea surface temperature field with two sharp gradients
Tâ‚€_func(x) = Î¸â‚€ + Î”T * sign(cos(2Ï€ * x / grid.Lx))
Tâ‚€ = Field{Center, Center, Nothing}(grid)
set!(Tâ‚€, Tâ‚€_func)

lines(Tâ‚€; axis = (xlabel = "SST (K)",))

# Saturation specific humidity at sea surface (use function, not field)
qáµ›â‚€_func(x) = saturation_specific_humidity(Tâ‚€_func(x), Ïâ‚€, constants, surface)
qáµ›â‚€ = Field{Center, Center, Nothing}(grid)
set!(qáµ›â‚€, qáµ›â‚€_func)

lines(qáµ›â‚€; axis = (xlabel = "qáµ›âº (kg/kg)",))

# We gather few parameters in a named tuple

parameters = (;
    Tâ‚€, qáµ›â‚€, Ïâ‚€,
    drag_coefficient = 1e-3,
    sensible_heat_transfer_coefficient = 1e-3,
    vapor_transfer_coefficient = 1e-3,
    gust_speed = 1e-2,  # Minimum wind speed (m/s)
)

# ## Boundary condition functions
#
# The boundary conditions compute surface fluxes using bulk aerodynamic formulas.
# For potential temperature thermodynamics, we specify fluxes for the potential
# temperature density Î¸ and moisture density Ïqáµ—.
#
# The flux formulas follow the standard bulk aerodynamic approach:
#
# ```math
# J_Ïˆ = - Ïâ‚€ C_Ïˆ U (Ï†â‚• - Ï†â‚€)
# ```
# where ``Ï†`` represents potential temperature or specific humidity, ``Cáµ©`` is the
# corresponding transfer coefficient, and ``U`` is the near-surface wind speed.
# More on the bulk formulae could be found at the
# [Interface fluxes section in ClimaOcean documentation](https://clima.github.io/ClimaOceanDocumentation/stable/interface_fluxes/).

# We need interpolation operators to compute wind speed at the appropriate
# grid locations for each flux calculation.

using Oceananigans.Operators: â„‘xyá¶ á¶œáµƒ, â„‘xyá¶œá¶ áµƒ, â„‘xá¶œáµƒáµƒ, â„‘yáµƒá¶œáµƒ

@inline Ï•Â²(i, j, k, grid, Ï•) = @inbounds Ï•[i, j, k]^2

@inline function sÂ²á¶ á¶œá¶œ(i, j, grid, fields)
    uÂ² = @inbounds fields.u[i, j, 1]^2
    vÂ² = â„‘xyá¶ á¶œáµƒ(i, j, 1, grid, Ï•Â², fields.v)
    return uÂ² + vÂ²
end

@inline function sÂ²á¶œá¶ á¶œ(i, j, grid, fields)
    uÂ² = â„‘xyá¶œá¶ áµƒ(i, j, 1, grid, Ï•Â², fields.u)
    vÂ² = @inbounds fields.v[i, j, 1]^2
    return uÂ² + vÂ²
end

@inline function sÂ²á¶œá¶œá¶œ(i, j, grid, fields)
    uÂ² = â„‘xá¶œáµƒáµƒ(i, j, 1, grid, Ï•Â², fields.u)
    vÂ² = â„‘yáµƒá¶œáµƒ(i, j, 1, grid, Ï•Â², fields.v)
    return uÂ² + vÂ²
end

# The momentum flux (surface stress) uses a quadratic drag law. The stress is
# proportional to the square of the wind speed, directed opposite to the
# near-surface velocity. A small "gust speed" prevents division by zero
# when winds are calm.

@inline function x_momentum_flux(i, j, grid, clock, fields, parameters)
    Ïu = @inbounds fields.Ïu[i, j, 1]
    U = sqrt(sÂ²á¶ á¶œá¶œ(i, j, grid, fields))
    Uáµ = parameters.gust_speed
    Å¨Â² = sÂ²á¶ á¶œá¶œ(i, j, grid, fields) + Uáµ^2
    Cá´° = parameters.drag_coefficient
    return - Cá´° * Å¨Â² * Ïu / U * (U > 0)
end

@inline function y_momentum_flux(i, j, grid, clock, fields, parameters)
    Ïv = @inbounds fields.Ïv[i, j, 1]
    U = sqrt(sÂ²á¶œá¶ á¶œ(i, j, grid, fields))
    Uáµ = parameters.gust_speed
    Å¨Â² = sÂ²á¶œá¶ á¶œ(i, j, grid, fields) + Uáµ^2
    Cá´° = parameters.drag_coefficient
    return - Cá´° * Å¨Â² * Ïv / U * (U > 0)
end

# The sensible heat flux transfers heat between the ocean surface and atmosphere.
# At the surface, the potential temperature approximately equals the temperature
# since the Exner function is close to unity at surface pressure.

@inline function potential_temperature_flux(i, j, grid, clock, fields, parameters)
    Î”Î¸ = @inbounds fields.Î¸[i, j, 1] - parameters.Tâ‚€[i, j, 1]

    Cáµ€ = parameters.sensible_heat_transfer_coefficient
    Uáµ = parameters.gust_speed
    Å¨ = sqrt(sÂ²á¶œá¶œá¶œ(i, j, grid, fields) + Uáµ^2)

    Ïâ‚€ = parameters.Ïâ‚€
    return - Ïâ‚€ * Cáµ€ * Å¨ * Î”Î¸
end

# The latent heat flux (moisture flux) transfers water vapor between the ocean
# and atmosphere. The ocean surface is assumed to be saturated at the SST,
# so the flux depends on the difference between the saturation specific humidity
# at the surface and the actual specific humidity in the near-surface air.

@inline function moisture_density_flux(i, j, grid, clock, fields, parameters)
    Î”q = @inbounds fields.qáµ—[i, j, 1] - parameters.qáµ›â‚€[i, j, 1]

    Cáµ› = parameters.vapor_transfer_coefficient
    Uáµ = parameters.gust_speed
    Å¨ = sqrt(sÂ²á¶œá¶œá¶œ(i, j, grid, fields) + Uáµ^2)

    Ïâ‚€ = parameters.Ïâ‚€
    return - Ïâ‚€ * Cáµ› * Å¨ * Î”q
end

# Assemble the boundary conditions for all prognostic variables.
# Each flux boundary condition uses `discrete_form=true` to access the
# grid indices directly, enabling efficient computation of spatially-varying fluxes.

Ïu_surface_flux = FluxBoundaryCondition(x_momentum_flux; discrete_form=true, parameters)
Ïv_surface_flux = FluxBoundaryCondition(y_momentum_flux; discrete_form=true, parameters)
ÏÎ¸_surface_flux = FluxBoundaryCondition(potential_temperature_flux; discrete_form=true, parameters)
Ïqáµ—_surface_flux = FluxBoundaryCondition(moisture_density_flux; discrete_form=true, parameters)

Ïu_bcs = FieldBoundaryConditions(bottom=Ïu_surface_flux)
Ïv_bcs = FieldBoundaryConditions(bottom=Ïv_surface_flux)
ÏÎ¸_bcs = FieldBoundaryConditions(bottom=ÏÎ¸_surface_flux)
Ïqáµ—_bcs = FieldBoundaryConditions(bottom=Ïqáµ—_surface_flux)

# ## Model construction
#
# We assemble the AtmosphereModel with all the components defined above.
# The model will solve the anelastic equations with the specified advection
# schemes, microphysics, and boundary conditions.

model = AtmosphereModel(grid; momentum_advection, scalar_advection, microphysics, formulation,
                        boundary_conditions = (Ïu=Ïu_bcs, Ïv=Ïv_bcs, ÏÎ¸=ÏÎ¸_bcs, Ïqáµ—=Ïqáµ—_bcs))

# ## Initial conditions
#
# We initialize the model with a uniform potential temperature equal to the
# reference value, creating a neutrally stratified atmosphere. A small
# background wind (1 m/s) in the x-direction provides initial momentum
# for the bulk flux calculations and helps break symmetry.

set!(model, Î¸=reference_state.potential_temperature, u=1)

# ## Simulation setup
#
# We configure the simulation to run for 4 hours with adaptive time stepping.
# The CFL condition limits the time step to maintain numerical stability,
# with a target CFL number of 0.7 providing a good balance between efficiency
# and accuracy.

simulation = Simulation(model, Î”t=10, stop_time=4hours)
conjure_time_step_wizard!(simulation, cfl=0.7)

# ## Diagnostic fields
#
# We define several diagnostic quantities for analysis and visualization:
# - Temperature T: the actual temperature field
# - Potential temperature Î¸: conserved in dry adiabatic processes
# - Liquid water content qË¡: mass fraction of cloud liquid water
# - Saturation specific humidity qáµ›âº: maximum water vapor the air can hold

T = model.temperature
Î¸ = liquid_ice_potential_temperature(model)
qË¡ = model.microphysical_fields.qË¡
qáµ›âº = Breeze.Microphysics.SaturationSpecificHumidity(model)

Ïu, Ïv, Ïw = model.momentum
u, v, w = model.velocities
qáµ— = model.specific_moisture

# ## Surface flux diagnostics
#
# We use Oceananigans' `BoundaryConditionOperation` to extract the surface flux
# values from the boundary conditions. These 1D fields (varying only in x)
# represent the actual flux values applied at the ocean-atmosphere interface.
#
# The surface fluxes are:
# - Ï„Ë£: surface momentum flux (stress), in kg mâ»Â¹ sâ»Â²
# - ğ’¬áµ€: sensible heat flux = cáµ–áµ Ã— Jáµ€, in W mâ»Â²
# - ğ’¬áµ›: latent heat flux = â„’Ë¡ Ã— Jáµ›, in W mâ»Â²
#
# where Jáµ€ is the temperature flux and Jáµ› is the moisture flux.

# Surface momentum flux
Ï„Ë£ = BoundaryConditionOperation(Ïu, :bottom, model)

# Sensible heat flux: ğ’¬áµ€ = cáµ–áµ Ã— Jáµ€
ÏÎ¸ = liquid_ice_potential_temperature_density(model)
cáµ–áµˆ = constants.dry_air.heat_capacity
cáµ–áµ› = constants.vapor.heat_capacity
cáµ–áµ = cáµ–áµˆ * (1 - qáµ›â‚€) + cáµ–áµ› * qáµ›â‚€
Jáµ€ = BoundaryConditionOperation(ÏÎ¸, :bottom, model)
ğ’¬áµ€ = cáµ–áµ * Jáµ€

# Latent heat flux: ğ’¬áµ› = â„’Ë¡ Ã— Jáµ›
Ïqáµ— = model.moisture_density
â„’Ë¡ = Breeze.Thermodynamics.liquid_latent_heat(Tâ‚€, constants)
Jáµ› = BoundaryConditionOperation(Ïqáµ—, :bottom, model)
ğ’¬áµ› = â„’Ë¡ * Jáµ›

# ## Progress callback
#
# A callback function prints diagnostic information every few iterations,
# helping monitor the simulation's progress and detect any numerical issues.

function progress(sim)
    qáµ— = sim.model.specific_moisture
    u, v, w = sim.model.velocities

    umax = maximum(abs, u)
    vmax = maximum(abs, v)
    wmax = maximum(abs, w)

    qáµ—min = minimum(qáµ—)
    qáµ—max = maximum(qáµ—)
    qË¡max = maximum(qË¡)

    Î¸min = minimum(Î¸)
    Î¸max = maximum(Î¸)

    msg = @sprintf("Iter: %d, t = %s, max|u|: (%.2e, %.2e, %.2e)",
                    iteration(sim), prettytime(sim), umax, vmax, wmax)

    msg *= @sprintf(", extrema(qáµ—): (%.2e, %.2e), max(qË¡): %.2e, extrema(Î¸): (%.2e, %.2e)",
                     qáµ—min, qáµ—max, qË¡max, Î¸min, Î¸max)

    @info msg

    return nothing
end

add_callback!(simulation, progress, IterationInterval(100))

# ## Output
#
# We save both the full 2D fields and the 1D surface flux fields.
# We include both native model variables and others like, e.g., the total speed,
# ``\sqrt{uÂ² + wÂ²}`` and the cross-stream vorticity ``âˆ‚_z u - âˆ‚_x w``.
# The JLD2 format provides efficient storage with full Julia type preservation.

output_filename = "prescribed_sst_convection.jld2"
qáµ— = model.specific_moisture
u, v, w, = model.velocities
s = sqrt(u^2 + w^2) # speed
Î¾ = âˆ‚z(u) - âˆ‚x(w)   # cross-stream vorticity
outputs = (; s, Î¾, T, Î¸, qË¡, qáµ›âº, qáµ—, Ï„Ë£, ğ’¬áµ€, ğ’¬áµ›, Î£ğ’¬=ğ’¬áµ€+ğ’¬áµ›)

ow = JLD2Writer(model, outputs;
                filename = output_filename,
                schedule = TimeInterval(2minutes),
                overwrite_existing = true)

simulation.output_writers[:jld2] = ow

# ## Run the simulation

@info "Running prescribed SST convection simulation..."
run!(simulation)

# ## Visualization
#
# We create animations showing the evolution of the flow fields. The figure
# displays velocity components (u, w), thermodynamic fields (Î¸, T),
# moisture fields (qáµ—, qË¡), and surface fluxes (momentum and heat).

@assert isfile(output_filename) "Output file $(output_filename) not found."

s_ts = FieldTimeSeries(output_filename, "s")
Î¾_ts = FieldTimeSeries(output_filename, "Î¾")
Î¸_ts = FieldTimeSeries(output_filename, "Î¸")
T_ts = FieldTimeSeries(output_filename, "T")
qáµ—_ts = FieldTimeSeries(output_filename, "qáµ—")
qË¡_ts = FieldTimeSeries(output_filename, "qË¡")
Ï„Ë£_ts = FieldTimeSeries(output_filename, "Ï„Ë£")
ğ’¬áµ€_ts = FieldTimeSeries(output_filename, "ğ’¬áµ€")
ğ’¬áµ›_ts = FieldTimeSeries(output_filename, "ğ’¬áµ›")
Î£ğ’¬_ts = FieldTimeSeries(output_filename, "Î£ğ’¬")

times = Î¸_ts.times
Nt = length(Î¸_ts)

n = Observable(Nt)

sn = @lift s_ts[$n]
Î¾n = @lift Î¾_ts[$n]
Î¸n = @lift Î¸_ts[$n]
qáµ—n = @lift qáµ—_ts[$n]
Tn = @lift T_ts[$n]
qË¡n = @lift qË¡_ts[$n]
Ï„Ë£n = @lift Ï„Ë£_ts[$n]
ğ’¬áµ€n = @lift ğ’¬áµ€_ts[$n]
ğ’¬áµ›n = @lift ğ’¬áµ›_ts[$n]
Î£ğ’¬n = @lift Î£ğ’¬_ts[$n]

# Now we are ready to plot.

fig = Figure(size=(800, 1000), fontsize=13)

title = @lift "t = $(prettytime(times[$n]))"

axs = Axis(fig[1, 1], ylabel="z (m)")
axÎ¾ = Axis(fig[1, 2])
axÎ¸ = Axis(fig[2, 1], ylabel="z (m)")
axq = Axis(fig[2, 2])
axT = Axis(fig[3, 1], ylabel="z (m)")
axqË¡ = Axis(fig[3, 2])

# Surface flux plots at bottom
axÏ„ = Axis(fig[4, 1], xlabel="x (m)", ylabel="Ï„Ë£ (kg mâ»Â¹ sâ»Â²)", title="Surface momentum flux")
axğ’¬ = Axis(fig[4, 2], xlabel="x (m)", ylabel="ğ’¬ (W mâ»Â²)", title="Surface heat flux (ğ’¬áµ€ + ğ’¬áµ›)")

fig[0, :] = Label(fig, title, fontsize=22, tellwidth=false)

# Compute color limits from the full time series
Î¸_limits = extrema(Î¸_ts)
T_limits = extrema(T_ts)
s_limits = (0, maximum(s_ts))
max_absÎ¾ = maximum(abs, Î¾_ts)
Î¾_limits = (-0.8*max_absÎ¾, 0.8*max_absÎ¾)

qáµ—_max = maximum(qáµ—_ts)
qË¡_max = maximum(qË¡_ts)

# Flux limits
Ï„Ë£_max = max(abs(minimum(Ï„Ë£_ts)), abs(maximum(Ï„Ë£_ts)))
ğ’¬_min = min(minimum(ğ’¬áµ€_ts), minimum(ğ’¬áµ›_ts), minimum(Î£ğ’¬_ts))
ğ’¬_max = max(maximum(ğ’¬áµ€_ts), maximum(ğ’¬áµ›_ts), maximum(Î£ğ’¬_ts))

hms = heatmap!(axs, sn, colorrange=s_limits, colormap=:speed)
hmÎ¾ = heatmap!(axÎ¾, Î¾n, colorrange=Î¾_limits, colormap=:balance)
hmÎ¸ = heatmap!(axÎ¸, Î¸n, colorrange=Î¸_limits, colormap=:thermal)
hmq = heatmap!(axq, qáµ—n, colorrange=(0, qáµ—_max), colormap=Reverse(:Purples_4))
hmT = heatmap!(axT, Tn, colorrange=T_limits)
hmqË¡ = heatmap!(axqË¡, qË¡n, colorrange=(0, qË¡_max), colormap=Reverse(:Blues_4))

# Plot the surface fluxes
lines!(axÏ„, Ï„Ë£n, color=:black, linewidth=2)

lines!(axğ’¬, ğ’¬áµ€n, color=:firebrick, linewidth=2, label="sensible")
lines!(axğ’¬, ğ’¬áµ›n, color=:blue, linewidth=2, label="latent")
lines!(axğ’¬, Î£ğ’¬n, color=:green, linewidth=4, label="total")
Legend(fig[4, 3], axğ’¬)

# Add zero lines, fix axis limits, and add colorbars.

for ax in (axÏ„, axğ’¬)
    lines!(ax, [-grid.Lx/2, grid.Lx/2], [0, 0], color=:grey, linestyle=:dash)
end

for ax in (axs, axÎ¾, axÎ¸, axq, axT, axqË¡, axÏ„, axğ’¬)
    xlims!(ax, -grid.Lx/2, grid.Lx/2)
end

ylims!(axÏ„, -Ï„Ë£_max, Ï„Ë£_max)
ylims!(axğ’¬, ğ’¬_min, ğ’¬_max)

Colorbar(fig[1, 0], hms, label="âˆš(uÂ² + wÂ²) (m/s)", flipaxis=false)
Colorbar(fig[1, 3], hmÎ¾, label="âˆ‚u/âˆ‚z - âˆ‚w/âˆ‚x (sâ»Â¹)")
Colorbar(fig[2, 0], hmÎ¸, label="Î¸ (K)", flipaxis=false)
Colorbar(fig[2, 3], hmq, label="qáµ— (kg/kg)")
Colorbar(fig[3, 0], hmT, label="T (K)", flipaxis=false)
Colorbar(fig[3, 3], hmqË¡, label="qË¡ (kg/kg)")

# Now we are ready to make a cool animation.

CairoMakie.record(fig, "prescribed_sst.mp4", 1:Nt, framerate=12) do nn
    n[] = nn
end
nothing #hide

# ![](prescribed_sst.mp4)
