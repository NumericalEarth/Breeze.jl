# # Tropical Cyclone World (Cronin & Chavas, 2019)
#
# This example implements the rotating radiative-convective equilibrium (RCE) experiment
# from [Cronin and Chavas (2019)](@cite Cronin2019). The experiment demonstrates that tropical cyclones can form
# and persist even in completely dry atmospheres, challenging the conventional wisdom
# that moisture is essential for TC dynamics.
#
# The key innovation is the surface wetness parameter Î², which controls the transition
# from completely dry (Î² = 0, no evaporation) to fully moist (Î² = 1) conditions.
# [Cronin and Chavas (2019)](@cite Cronin2019) found that TCs form in both limits, with a "no-storms-land" at
# intermediate Î² where spontaneous genesis does not occur.  This script defaults to Î² =
# 1 (moist), which produces robust spontaneous TC genesis at moderate resolution. The
# simulation approximates the paper's 100-day nonrotating RCE spinup with an
# equilibrated initial temperature profile (dry adiabat in the troposphere, isothermal
# stratosphere) and uses warm-phase saturation adjustment microphysics for the moist
# case.

using Breeze
using Breeze.Thermodynamics: compute_reference_state!
using Oceananigans: Oceananigans
using Oceananigans.Units

using CairoMakie
using CUDA
using Printf
using Random

Random.seed!(2019)
Oceananigans.defaults.FloatType = Float32

# ## Domain and grid
#
# [Cronin and Chavas (2019)](@cite Cronin2019) used a 1152 km Ã— 1152 km domain with 2 km horizontal
# resolution. To reduce computational costs for the purpose of this example, we use a
# 288 km Ã— 288 km domain -- 4x smaller in both horizontal directions -- with a
# 2x coarser 4 km horizontal resolution. We keep the 28 km model top,
# but with 40 m spacing in the lowest kilometers rather than ~16 m, and
# 1000 m spacing above 3.5 km rather than 500 m (and a smooth transition in between).

arch = GPU()
paper_Lx = 1152e3
paper_Nx = 576
Lx = Ly = paper_Lx / 4
Nx = Ny = paper_Nx / 8 |> Int
H = 28e3

Î”z_fine = 40 # m
Î”z_coarse = 1000 # m

z = PiecewiseStretchedDiscretization(
    z  = [0, 1000, 3500, H],
    Î”z = [Î”z_fine, Î”z_fine, Î”z_coarse, Î”z_coarse])

Nz = length(z) - 1

grid = RectilinearGrid(arch; size = (Nx, Ny, Nz), halo = (5, 5, 5),
                       x = (0, Lx), y = (0, Ly), z,
                       topology = (Periodic, Periodic, Bounded))

# ## Reference state and dynamics
#
# We use the anelastic formulation with a reference state initialized from
# the surface potential temperature Tâ‚€ = 300 K and standard surface pressure.
# The reference state is then adjusted to match the initial temperature and
# moisture profiles. This adjustment is critical for tall domains: without it,
# the constant-Î¸ adiabat reference state diverges from the actual atmosphere
# in the stratosphere (T_ref â‰ˆ 26 K vs T_actual = 210 K at 28 km), producing
# catastrophic buoyancy forces.

Tâ‚€ = 300
constants = ThermodynamicConstants()

reference_state = ReferenceState(grid, constants;
                                 surface_pressure = 101325,
                                 potential_temperature = Tâ‚€,
                                 vapor_mass_fraction = 0)

# Define equilibrium temperature and moisture profiles for adjustment and initialization
Táµ—Ë¢ = 210
cáµ–áµˆ = constants.dry_air.heat_capacity
g = constants.gravitational_acceleration
Ráµˆ = Breeze.Thermodynamics.dry_air_gas_constant(constants)
Îº = Ráµˆ / cáµ–áµˆ
pË¢áµ— = reference_state.standard_pressure
Î â‚€ = (101325 / pË¢áµ—)^Îº

# Analytical Exner function for a hydrostatic constant-Î¸ atmosphere
Î (z) = Î â‚€ - g * z / (cáµ–áµˆ * Tâ‚€)

Î² = 1
qâ‚€ = 15e-3 # surface specific humidity (kg/kg)
Hq = 3000   # moisture scale height (m)

Táµ‡áµ(z) = max(Táµ—Ë¢, Tâ‚€ * Î (z))
qáµ‡áµ(z) = max(0, Î² * qâ‚€ * exp(-z / Hq))

# Adjust reference state to match actual profiles
compute_reference_state!(reference_state, Táµ‡áµ, qáµ‡áµ, constants)

dynamics = AnelasticDynamics(reference_state)
coriolis = FPlane(f = 3e-4)

# ## Surface fluxes
#
# Following the paper's bulk formulas (Eqs. 2-4), with drag coefficient
# Cá´° = 1.5 Ã— 10â»Â³ and gustiness vâ˜… = 1 m/s. The surface wetness parameter Î²
# scales the moisture flux coefficient.

Cá´° = Cáµ€ = 1.5e-3
Uáµ = 1

Ïu_bcs = FieldBoundaryConditions(bottom = BulkDrag(coefficient = Cá´°, gustiness = Uáµ))
Ïv_bcs = FieldBoundaryConditions(bottom = BulkDrag(coefficient = Cá´°, gustiness = Uáµ))

Ïe_bcs = FieldBoundaryConditions(bottom = BulkSensibleHeatFlux(coefficient = Cáµ€,
                                                               gustiness = Uáµ,
                                                               surface_temperature = Tâ‚€))

Ïqáµ—_bcs = FieldBoundaryConditions(bottom = BulkVaporFlux(coefficient = Î²*Cáµ€,
                                                         gustiness = Uáµ,
                                                         surface_temperature = Tâ‚€))

boundary_conditions = (; Ïu=Ïu_bcs, Ïv=Ïv_bcs, Ïe=Ïe_bcs, Ïqáµ—=Ïqáµ—_bcs)
nothing #hide

# ## Radiative forcing
#
# The paper (Eq. 1) prescribes a piecewise radiative tendency: constant cooling
# at á¹ª = 1 K/day for T > Táµ—Ë¢ (troposphere), and Newtonian relaxation toward Táµ—Ë¢
# with timescale Ï„áµ£ = 20 days for T â‰¤ Táµ—Ë¢ (stratosphere). We apply this as an
# energy forcing on Ïe, so that Breeze handles the conversion to ÏÎ¸ tendency.

á¹ª  = 1 / day
Ï„áµ£ = 20days
Ïáµ£ = reference_state.density
parameters = (; Táµ—Ë¢, á¹ª, Ï„áµ£, Ïáµ£, cáµ–áµˆ)

@inline function Ïe_forcing_func(i, j, k, grid, clock, model_fields, p)
    @inbounds T = model_fields.T[i, j, k]
    @inbounds Ï = p.Ïáµ£[i, j, k]
    âˆ‚Tâˆ‚t = ifelse(T > p.Táµ—Ë¢, -p.á¹ª, (p.Táµ—Ë¢ - T) / p.Ï„áµ£)
    return Ï * p.cáµ–áµˆ * âˆ‚Tâˆ‚t
end

Ïe_forcing = Forcing(Ïe_forcing_func; discrete_form=true, parameters)

# ## Sponge layer
#
# Rayleigh damping with a Gaussian profile centered at 26 km (width 2 km)
# prevents spurious wave reflections from the rigid lid.

sponge_mask = GaussianMask{:z}(center=26000, width=2000)
Ïw_sponge = Relaxation(rate=1/30, mask=sponge_mask)

forcing = (; Ïe=Ïe_forcing, Ïw=Ïw_sponge)
nothing #hide

# ## Model
#
# We use 9th-order WENO advection and warm-phase saturation adjustment microphysics.

momentum_advection = WENO(order=9)
scalar_advection = (ÏÎ¸ = WENO(order=5),
                    Ïqáµ— = WENO(order=5, bounds=(0, 1)))

microphysics = SaturationAdjustment(equilibrium=WarmPhaseEquilibrium())

model = AtmosphereModel(grid; dynamics, coriolis, momentum_advection, scalar_advection,
                        microphysics, forcing, boundary_conditions)

# ## Initial conditions
#
# We initialize with an equilibrated temperature profile: a dry adiabat in the
# troposphere transitioning to an isothermal stratosphere at Táµ—Ë¢ = 210 K.
# This approximates the paper's 100-day nonrotating RCE spinup. Small random
# perturbations in the lowest kilometer trigger convection.
#
# **Important:** After `compute_reference_state!`, we must use `set!(model, T=...)` rather than
# `set!(model, Î¸=...)`. The `compute_reference_state!` call recomputes the reference pressure,
# which changes the Exner function used to convert Î¸ â†’ T. Setting Î¸ directly
# would produce incorrect temperatures in the stratosphere.

Î´T = 1//2  # K perturbation amplitude
zÎ´ = 1000  # m perturbation depth
Î´q = 1e-4  # moisture perturbation amplitude (kg/kg)

Táµ¢(x, y, z) = Táµ‡áµ(z) + Î´T * (2rand() - 1) * (z < zÎ´)
qáµ—áµ¢(x, y, z) = max(0, qáµ‡áµ(z) + Î´q * (2rand() - 1) * (z < zÎ´))

set!(model, T = Táµ¢, qáµ— = qáµ—áµ¢)

# ## Simulation
#
# We run for 4 days, which is sufficient for moist TC genesis and intensification.

simulation = Simulation(model; Î”t=1, stop_time=4days)
conjure_time_step_wizard!(simulation, cfl=0.7)

# ## Output and progress

u, v, w = model.velocities
Î¸ = liquid_ice_potential_temperature(model)
s = @at (Center, Center, Center) sqrt(u^2 + v^2)
sâ‚€ = Field(s, indices = (:, :, 1))

Ïqáµ— = model.moisture_density
Ïe = static_energy_density(model)
â„’Ë¡ = Breeze.Thermodynamics.liquid_latent_heat(Tâ‚€, constants)
ğ’¬áµ€ = BoundaryConditionOperation(Ïe, :bottom, model)
Jáµ› = BoundaryConditionOperation(Ïqáµ—, :bottom, model)
ğ’¬ = Field(ğ’¬áµ€ + â„’Ë¡ * Jáµ›)

function progress(sim)
    compute!(sâ‚€)
    compute!(ğ’¬)
    umax = maximum(abs, u)
    vmax = maximum(abs, v)
    wmax = maximum(abs, w)
    sâ‚€max = maximum(sâ‚€)
    ğ’¬max = maximum(ğ’¬)
    Î¸min, Î¸max = extrema(Î¸)
    msg = @sprintf("(%d) t = %s, Î”t = %s",
                   iteration(sim), prettytime(sim, false), prettytime(sim.Î”t, false))
    msg *= @sprintf(", sâ‚€ = %.1f m/s, max(ğ’¬) = %.1f W/mÂ², max|U| â‰ˆ (%d, %d, %d) m/s, Î¸ âˆˆ [%d, %d] K",
                    sâ‚€max, ğ’¬max, umax, vmax, wmax, floor(Î¸min), ceil(Î¸max))
    @info msg
    return nothing
end

add_callback!(simulation, progress, IterationInterval(1000))

# Horizontally-averaged profiles.

qáµ— = specific_prognostic_moisture(model)
â„‹ = RelativeHumidity(model)

avg_outputs = (Î¸ = Average(Î¸, dims=(1, 2)),
               qáµ— = Average(qáµ—, dims=(1, 2)),
               â„‹ = Average(â„‹, dims=(1, 2)),
               wÂ² = Average(w^2, dims=(1, 2)),
               wÎ¸ = Average(w * Î¸, dims=(1, 2)),
               wqáµ— = Average(w * qáµ—, dims=(1, 2)))

function save_parameters(file, model)
    file["parameters/Î²"] = Î²
    file["parameters/Tâ‚€"] = Tâ‚€
    file["parameters/Táµ—Ë¢"] = Táµ—Ë¢
    file["parameters/á¹ª"] = á¹ª
    file["parameters/fâ‚€"] = 3e-4
    file["parameters/Cá´°"] = Cá´°
    file["parameters/Nx"] = Nx
    file["parameters/Nz"] = Nz
end

simulation.output_writers[:profiles] = JLD2Writer(model, avg_outputs;
                                                  filename = "tc_world_profiles.jld2",
                                                  schedule = TimeInterval(1day),
                                                  init = save_parameters,
                                                  overwrite_existing = true)

# Surface fields for tracking TC development.

surface_outputs = (; s, ğ’¬)
simulation.output_writers[:surface] = JLD2Writer(model, surface_outputs;
                                                 filename = "tc_world_surface.jld2",
                                                 indices = (:, :, 1),
                                                 schedule = TimeInterval(30minutes),
                                                 overwrite_existing = true)

# ## Run

run!(simulation)

# ## Results: mean profile evolution
#
# Evolution of horizontally-averaged potential temperature, vertical velocity variance,
# and the vertical potential temperature flux.

Î¸t = FieldTimeSeries("tc_world_profiles.jld2", "Î¸")
qáµ—t = FieldTimeSeries("tc_world_profiles.jld2", "qáµ—")
â„‹t = FieldTimeSeries("tc_world_profiles.jld2", "â„‹")
wÂ²t = FieldTimeSeries("tc_world_profiles.jld2", "wÂ²")
wÎ¸t = FieldTimeSeries("tc_world_profiles.jld2", "wÎ¸")
wqáµ—t = FieldTimeSeries("tc_world_profiles.jld2", "wqáµ—")

times = Î¸t.times
Nt = length(times)

fig = Figure(size=(900, 400), fontsize=10)

axÎ¸ = Axis(fig[1, 1], xlabel="Î¸ (K)", ylabel="z (m)")
axqáµ— = Axis(fig[1, 2], xlabel="qáµ— (kg/kg)")
axâ„‹ = Axis(fig[1, 3], xlabel="â„‹")
axwÂ² = Axis(fig[1, 4], xlabel="wÂ² (mÂ²/sÂ²)")
axwÎ¸ = Axis(fig[1, 5], xlabel="wÎ¸ (mÂ²/sÂ² K)")
axwqáµ— = Axis(fig[1, 6], xlabel="wqáµ— (mÂ²/sÂ² kg/kg)", ylabel="z (m)", yaxisposition=:right)

default_colours = Makie.wong_colors()
colors = [default_colours[mod1(n, length(default_colours))] for n in 1:Nt]
linewidth = 3
alpha = 0.6

for n in 1:Nt
    label = n == 1 ? "initial" : "t = $(prettytime(times[n]))"
    lines!(axÎ¸, Î¸t[n], color=colors[n]; label, linewidth, alpha)
    lines!(axqáµ—, qáµ—t[n], color=colors[n]; linewidth, alpha)
    lines!(axâ„‹, â„‹t[n], color=colors[n]; linewidth, alpha)
    lines!(axwÂ², wÂ²t[n], color=colors[n]; linewidth, alpha)
    lines!(axwÎ¸, wÎ¸t[n], color=colors[n]; linewidth, alpha)
    lines!(axwqáµ—, wqáµ—t[n], color=colors[n]; linewidth, alpha)
end

for ax in (axqáµ—, axâ„‹, axwÂ², axwÎ¸)
    hideydecorations!(ax, grid=false)
    hidespines!(ax, :t, :r, :l)
end

hidespines!(axÎ¸, :t, :r)
hidespines!(axwqáµ—, :t, :l)
xlims!(axâ„‹, -0.1, 1.1)

Legend(fig[2, :], axÎ¸, labelsize=12, orientation=:horizontal)

fig[0, :] = Label(fig, "TC World (Î² = $Î²): mean profile evolution",
                  fontsize=16, tellwidth=false)

save("tc_world_profiles.png", fig) #src
fig

# ## Surface wind speed snapshots
#
# Snapshots of the surface wind speed field at early, middle, and late times
# show the evolution of convective organization and TC formation.

s_ts = FieldTimeSeries("tc_world_surface.jld2", "s")
ğ’¬_ts = FieldTimeSeries("tc_world_surface.jld2", "ğ’¬")

times = s_ts.times
Nt = length(times)

smax = maximum(s_ts)
slim = smax / 2
ğ’¬lim = maximum(ğ’¬_ts) / 8

fig = Figure(size=(1200, 800), fontsize=12)

s_heatmaps = []
ğ’¬_heatmaps = []
indices = ceil.(Int, [Nt / 3, 2Nt / 3, Nt])

for (i, idx) in enumerate(indices)
    xlabel = i == 1 ? "x (m)" : ""
    ylabel = i == 1 ? "y (m)" : ""
    title = "t = $(prettytime(times[idx]))"
    axs = Axis(fig[1, i]; aspect = 1, xlabel, ylabel, title)
    axğ’¬ = Axis(fig[2, i]; aspect = 1, xlabel, ylabel, title)
    s_hm = heatmap!(axs, s_ts[idx]; colormap=:speed, colorrange=(0, slim))
    push!(s_heatmaps, s_hm)
    ğ’¬_hm = heatmap!(axğ’¬, ğ’¬_ts[idx]; colormap=:magma, colorrange=(0, ğ’¬lim))
    push!(ğ’¬_heatmaps, ğ’¬_hm)
end

Colorbar(fig[1, length(indices) + 1], s_heatmaps[end]; label="Surface wind speed (m/s)")
Colorbar(fig[2, length(indices) + 1], ğ’¬_heatmaps[end]; label="Surface moisture flux (W/mÂ²)")

fig[0, :] = Label(fig, "TC World (Î² = $Î²): surface wind and heat flux",
                  fontsize=16, tellwidth=false)

save("tc_world_surface.png", fig) #src
fig

# ## Animation of surface wind speed

fig = Figure(size=(600, 550), fontsize=14)
ax = Axis(fig[1, 1]; xlabel="x (m)", ylabel="y (m)", aspect=1)

n = Observable(1)
title = @lift "TC World (Î² = $Î²) â€” t = $(prettytime(times[$n]))"
sn = @lift s_ts[$n]

hm = heatmap!(ax, sn; colormap=:speed, colorrange=(0, slim))
Colorbar(fig[1, 2], hm; label="Surface wind speed (m/s)")
fig[0, :] = Label(fig, title, fontsize=16, tellwidth=false)

CairoMakie.record(fig, "tc_world.mp4", 1:Nt, framerate=16) do nn
    n[] = nn
end
nothing #hide

# ![](tc_world.mp4)

# ## Discussion
#
# This example demonstrates spontaneous tropical cyclone genesis in a rotating
# radiative-convective equilibrium setup, following [Cronin2019](@citet).
# The surface wetness parameter Î² controls moisture availability: Î² = 1 (default)
# produces robust moist TC genesis, while Î² = 0 yields dry TCs.
#
# The radiative forcing is a piecewise temperature tendency: constant cooling
# at 1 K/day in the troposphere (T > Táµ—Ë¢) and Newtonian relaxation toward Táµ—Ë¢
# with timescale Ï„áµ£ = 20 days in the stratosphere. Surface fluxes use bulk
# formulas with drag coefficient Cá´° = 1.5 Ã— 10â»Â³ and gustiness 1 m/s.
# The f-plane Coriolis parameter is fâ‚€ = 3 Ã— 10â»â´ sâ»Â¹.
