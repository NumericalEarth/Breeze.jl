using ..Thermodynamics:
    MoistureMassFractions,
    mixture_heat_capacity,
    dry_air_gas_constant,
    vapor_gas_constant,
    PlanarLiquidSurface,
    PlanarMixedPhaseSurface,
    saturation_vapor_pressure,
    density,
    temperature,
    is_absolute_zero,
    with_moisture,
    total_specific_moisture,
    AbstractThermodynamicState

using Oceananigans: Oceananigans, CenterField
using DocStringExtensions: TYPEDSIGNATURES

import ..Thermodynamics: saturation_specific_humidity

abstract type AbstractEquilibrium end

struct SaturationAdjustment{E, FT}
    tolerance :: FT
    maxiter :: FT
    equilibrium :: E
end

const SA = SaturationAdjustment

"""
$(TYPEDSIGNATURES)

Return `SaturationAdjustment` microphysics representing an instantaneous adjustment
to `equilibrium` between condensates and water vapor, computed by a solver with
`tolerance` and `maxiter`.

The options for `equilibrium` are:

* [`WarmPhaseEquilibrium()`](@ref WarmPhaseEquilibrium) representing an equilibrium between
  water vapor and liquid water.

* [`MixedPhaseEquilibrium()`](@ref MixedPhaseEquilibrium) representing a temperature-dependent
  equilibrium between water vapor, possibly supercooled liquid water, and ice. The equilibrium
  state is modeled as a linear variation of the equilibrium liquid fraction with temperature,
  between the freezing temperature (e.g. 273.15 K) below which liquid water is supercooled,
  and the temperature of homogeneous ice nucleation temperature (e.g. 233.15 K) at which
  the supercooled liquid fraction vanishes.
"""
function SaturationAdjustment(FT::DataType=Oceananigans.defaults.FloatType;
                              tolerance = 1e-3,
                              maxiter = Inf,
                              equilibrium = MixedPhaseEquilibrium(FT))
    tolerance = convert(FT, tolerance)
    maxiter = convert(FT, maxiter)
    return SaturationAdjustment(tolerance, maxiter, equilibrium)
end

@inline microphysical_velocities(::SaturationAdjustment, name) = nothing

#####
##### Warm-phase equilibrium
#####

"""
$(TYPEDSIGNATURES)

Return `WarmPhaseEquilibrium` representing an equilibrium between water vapor and liquid water.
"""
struct WarmPhaseEquilibrium <: AbstractEquilibrium end
@inline equilibrated_surface(::WarmPhaseEquilibrium, T) = PlanarLiquidSurface()

@inline function equilibrated_moisture_mass_fractions(T, qáµ—, qáµ›âº, ::WarmPhaseEquilibrium)
    qË¡ = max(0, qáµ— - qáµ›âº)
    qáµ› = qáµ— - qË¡
    return MoistureMassFractions(qáµ›, qË¡)
end

#####
##### Mixed-phase equilibrium
#####

struct MixedPhaseEquilibrium{FT} <: AbstractEquilibrium
    freezing_temperature :: FT
    homogeneous_ice_nucleation_temperature :: FT
end

"""
$(TYPEDSIGNATURES)

Return `MixedPhaseEquilibrium` representing a temperature-dependent equilibrium between
water vapor, possibly supercooled liquid water, and ice.

The equilibrium state is modeled as a linear variation of the equilibrium liquid fraction with temperature,
between the freezing temperature (e.g. 273.15 K) below which liquid water is supercooled,
and the temperature of homogeneous ice nucleation temperature (e.g. 233.15 K) at which
the supercooled liquid fraction vanishes.
"""
function MixedPhaseEquilibrium(FT = Oceananigans.defaults.FloatType;
                               freezing_temperature = 273.15,
                               homogeneous_ice_nucleation_temperature = 233.15)

    if freezing_temperature < homogeneous_ice_nucleation_temperature
        throw(ArgumentError("`freezing_temperature` must be greater than `homogeneous_ice_nucleation_temperature`"))
    end

    freezing_temperature = convert(FT, freezing_temperature)
    homogeneous_ice_nucleation_temperature = convert(FT, homogeneous_ice_nucleation_temperature)
    return MixedPhaseEquilibrium(freezing_temperature, homogeneous_ice_nucleation_temperature)
end

@inline function equilibrated_surface(equilibrium::MixedPhaseEquilibrium, T)
    Tá¶  = equilibrium.freezing_temperature
    TÊ° = equilibrium.homogeneous_ice_nucleation_temperature
    Tâ€² = clamp(T, TÊ°, Tá¶ )
    Î» = (Tâ€² - TÊ°) / (Tá¶  - TÊ°)
    return PlanarMixedPhaseSurface(Î»)
end

@inline function equilibrated_moisture_mass_fractions(T, qáµ—, qáµ›âº, equilibrium::MixedPhaseEquilibrium)
    surface = equilibrated_surface(equilibrium, T)
    Î» = surface.liquid_fraction
    qá¶œ = max(0, qáµ— - qáµ›âº)
    qáµ› = qáµ— - qá¶œ
    qË¡ = Î» * qá¶œ
    qâ± = (1 - Î») * qá¶œ
    return MoistureMassFractions(qáµ›, qË¡, qâ±)
end

const WarmPhaseSaturationAdjustment{FT} = SaturationAdjustment{WarmPhaseEquilibrium, FT} where FT
const MixedPhaseSaturationAdjustment{FT} = SaturationAdjustment{MixedPhaseEquilibrium{FT}, FT} where FT

const WPSA = WarmPhaseSaturationAdjustment
const MPSA = MixedPhaseSaturationAdjustment

prognostic_field_names(::WPSA) = tuple()
prognostic_field_names(::MPSA) = tuple()

center_field_tuple(grid, names...) = NamedTuple{names}(CenterField(grid) for name in names)
materialize_microphysical_fields(::WPSA, grid, bcs) = center_field_tuple(grid, :qáµ›, :qË¡)
materialize_microphysical_fields(::MPSA, grid, bcs) = center_field_tuple(grid, :qáµ›, :qË¡, :qâ±)

@inline @inbounds function update_microphysical_fields!(Î¼, ::WPSA, i, j, k, grid, density, ð’°, thermo)
    Î¼.qáµ›[i, j, k] = ð’°.moisture_mass_fractions.vapor
    Î¼.qË¡[i, j, k] = ð’°.moisture_mass_fractions.liquid
    return nothing
end

@inline @inbounds function update_microphysical_fields!(Î¼, ::MPSA, i, j, k, grid, density, ð’°, thermo)
    Î¼.qáµ›[i, j, k] = ð’°.moisture_mass_fractions.vapor
    Î¼.qË¡[i, j, k] = ð’°.moisture_mass_fractions.liquid
    Î¼.qâ±[i, j, k] = ð’°.moisture_mass_fractions.ice
    return nothing
end

@inline @inbounds function compute_moisture_fractions(i, j, k, grid, ::WPSA, Ï, qáµ—, Î¼)
    qáµ› = Î¼.qáµ›[i, j, k]
    qË¡ = Î¼.qË¡[i, j, k]
    return MoistureMassFractions(qáµ›, qË¡)
end

@inline @inbounds function compute_moisture_fractions(i, j, k, grid, ::MPSA, Ï, qáµ—, Î¼)
    qáµ› = Î¼.qáµ›[i, j, k]
    qË¡ = Î¼.qË¡[i, j, k]
    qâ± = Î¼.qâ±[i, j, k]
    return MoistureMassFractions(qáµ›, qË¡, qâ±)
end

@inline microphysical_tendency(i, j, k, grid, ::SA, args...) = zero(grid)

#####
##### Saturation adjustment utilities
#####

@inline function saturation_specific_humidity(T, Ï, thermo, equilibrium::AbstractEquilibrium)
    surface = equilibrated_surface(equilibrium, T)
    return saturation_specific_humidity(T, Ï, thermo, surface)
end

@inline function adjustment_saturation_specific_humidity(T, páµ£, qáµ—, thermo, equil)
    surface = equilibrated_surface(equil, T)
    páµ›âº = saturation_vapor_pressure(T, thermo, surface)
    Ráµˆ = dry_air_gas_constant(thermo)
    Ráµ› = vapor_gas_constant(thermo)
    Ïµáµˆáµ› = Ráµˆ / Ráµ›
    return Ïµáµˆáµ› * (1 - qáµ—) * páµ›âº / (páµ£ - páµ›âº)
end

@inline function adjust_state(ð’°â‚€, T, thermo, equilibrium)
    páµ£ = ð’°â‚€.reference_pressure
    qáµ— = total_specific_moisture(ð’°â‚€)
    qáµ›âº = adjustment_saturation_specific_humidity(T, páµ£, qáµ—, thermo, equilibrium)
    qâ‚ = equilibrated_moisture_mass_fractions(T, qáµ—, qáµ›âº, equilibrium)
    return with_moisture(ð’°â‚€, qâ‚)
end

@inline function saturation_adjustment_residual(T, ð’°â‚€, thermo, equilibrium)
    ð’°â‚ = adjust_state(ð’°â‚€, T, thermo, equilibrium)
    Tâ‚ = temperature(ð’°â‚, thermo)
    return T - Tâ‚
end

const ATC = AbstractThermodynamicState

# This function allows saturation adjustment to be used as a microphysics scheme directly
@inline function maybe_adjust_thermodynamic_state(ð’°â‚€, saturation_adjustment::SA, microphysical_fields, qáµ—, thermo)
    qáµƒ = MoistureMassFractions(qáµ—) # compute moisture state to be adjusted
    ð’°áµƒ = with_moisture(ð’°â‚€, qáµƒ)
    return adjust_thermodynamic_state(ð’°áµƒ, saturation_adjustment, thermo)
end

"""
$(TYPEDSIGNATURES)

Return the saturation-adjusted thermodynamic state using a secant iteration.
"""
@inline function adjust_thermodynamic_state(ð’°â‚€::ATC, microphysics::SA, thermo)
    FT = eltype(ð’°â‚€)
    is_absolute_zero(ð’°â‚€) && return ð’°â‚€

    # Compute an initial guess assuming unsaturated conditions
    qáµ— = total_specific_moisture(ð’°â‚€)
    qâ‚ = MoistureMassFractions(qáµ—)
    ð’°â‚ = with_moisture(ð’°â‚€, qâ‚)
    Tâ‚ = temperature(ð’°â‚, thermo)

    equilibrium = microphysics.equilibrium
    páµ£ = ð’°â‚€.reference_pressure
    Ïâ‚ = density(páµ£, Tâ‚, qâ‚, thermo)
    qáµ›âºâ‚ = saturation_specific_humidity(Tâ‚, Ïâ‚, thermo, equilibrium)
    qáµ— <= qáµ›âºâ‚ && return ð’°â‚

    # If we made it here, the state is saturated.
    # So, we re-initialize our first guess assuming saturation
    ð’°â‚ = adjust_state(ð’°â‚€, Tâ‚, thermo, equilibrium)

    # Next, we generate a second guess that scaled by the supersaturation implied by Tâ‚
    â„’Ë¡áµ£ = thermo.liquid.reference_latent_heat
    â„’â±áµ£ = thermo.ice.reference_latent_heat
    qË¡â‚ = qâ‚.liquid
    qâ±â‚ = qâ‚.ice
    cáµ–áµ = mixture_heat_capacity(qâ‚, thermo)
    Î”T = (â„’Ë¡áµ£ * qË¡â‚ + â„’â±áµ£ * qâ±â‚) / cáµ–áµ
    ÏµT = convert(FT, 0.01) # minimum increment for second guess
    Tâ‚‚ = Tâ‚ + max(ÏµT, Î”T / 2) # reduce the increment, recognizing it is an overshoot
    ð’°â‚‚ = adjust_state(ð’°â‚, Tâ‚‚, thermo, equilibrium)

    # Initialize secant iteration
    râ‚ = saturation_adjustment_residual(Tâ‚, ð’°â‚, thermo, equilibrium)
    râ‚‚ = saturation_adjustment_residual(Tâ‚‚, ð’°â‚‚, thermo, equilibrium)
    Î´ = microphysics.tolerance
    iter = 0

    while abs(râ‚‚) > Î´ && iter < microphysics.maxiter
        # Compute slope
        Î”TÎ”r = (Tâ‚‚ - Tâ‚) / (râ‚‚ - râ‚)

        # Store previous values
        râ‚ = râ‚‚
        Tâ‚ = Tâ‚‚
        ð’°â‚ = ð’°â‚‚

        # Update
        Tâ‚‚ -= râ‚‚ * Î”TÎ”r
        ð’°â‚‚ = adjust_state(ð’°â‚‚, Tâ‚‚, thermo, equilibrium)
        râ‚‚ = saturation_adjustment_residual(Tâ‚‚, ð’°â‚‚, thermo, equilibrium)
        iter += 1
    end

    return ð’°â‚‚
end

"""
    $(TYPEDSIGNATURES)

Perform saturation adjustment and return the temperature
associated with the adjusted state.
"""
function compute_temperature(ð’°â‚€, adjustment::SA, thermo)
    ð’°â‚ = adjust_thermodynamic_state(ð’°â‚€, adjustment, thermo)
    return temperature(ð’°â‚, thermo)
end

# When no microphysics adjustment is needed
compute_temperature(ð’°â‚€, ::Nothing, thermo) = temperature(ð’°â‚€, thermo)
