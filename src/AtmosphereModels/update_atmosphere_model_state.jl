using ..Thermodynamics:
    Thermodynamics,
    mixture_heat_capacity,
    mixture_gas_constant

using Oceananigans.BoundaryConditions: fill_halo_regions!, compute_x_bcs!, compute_y_bcs!, compute_z_bcs!
using Oceananigans.TurbulenceClosures: compute_diffusivities!
using Oceananigans.ImmersedBoundaries: mask_immersed_field!
using Oceananigans.TimeSteppers: TimeSteppers
using Oceananigans.Utils: launch!

# AnelasticModel type alias imported from AnelasticFormulation submodule

function TimeSteppers.update_state!(model::AnelasticModel, callbacks=[]; compute_tendencies=true)
    tracer_density_to_specific!(model) # convert tracer density to specific tracer distribution
    fill_halo_regions!(prognostic_fields(model), model.clock, fields(model), async=true)
    compute_auxiliary_variables!(model)
    update_radiation!(model.radiation, model)
    compute_forcings!(model)
    compute_tendencies && compute_tendencies!(model)
    tracer_specific_to_density!(model) # convert specific tracer distribution to tracer density
    return nothing
end

#####
##### Compute forcing-specific quantities (e.g., horizontal averages for subsidence)
#####

"""
    compute_forcings!(model)

Compute forcing-specific quantities needed before tendency calculation.
For example, `SubsidenceForcing` requires horizontal averages of the
fields being advected.
"""
function compute_forcings!(model)
    for forcing in model.forcing
        compute_forcing!(forcing)
    end
    return nothing
end

tracer_density_to_specific!(model) = tracer_density_to_specific!(model.tracers, formulation_density(model.formulation))
tracer_specific_to_density!(model) = tracer_specific_to_density!(model.tracers, formulation_density(model.formulation))

function tracer_density_to_specific!(tracers, density)
    # TODO: do all tracers a single kernel
    for œÅc in tracers
        parent(œÅc) ./= parent(density)
    end
    return nothing
end

function tracer_specific_to_density!(tracers, density)
    # TODO: do all tracers a single kernel
    for c in tracers
        parent(c) .*= parent(density)
    end
    return nothing
end

"""
$(TYPEDSIGNATURES)

Compute auxiliary model variables:

- velocities from momentum and density (eg ``u = œÅu / œÅ``)

- thermodynamic variables from the prognostic thermodynamic state,
    * temperature ``T``, possibly involving saturation adjustment
    * specific thermodynamic variable (``e = œÅe / œÅ`` or ``Œ∏ = œÅŒ∏ / œÅ``)
    * moisture mass fraction ``q·µó = œÅq·µó / œÅ``
"""
function compute_auxiliary_variables!(model)
    grid = model.grid
    arch = grid.architecture

    launch!(arch, grid, :xyz,
            _compute_velocities!,
            model.velocities,
            grid,
            model.formulation,
            model.momentum)

    fill_halo_regions!(model.velocities)
    foreach(mask_immersed_field!, model.velocities)

    # Dispatch on thermodynamics type
    compute_auxiliary_thermodynamic_variables!(model)

    # Compute diffusivities
    compute_diffusivities!(model.closure_fields, model.closure, model)

    # TODO: should we mask the auxiliary variables? They can also be masked in the kernel

    return nothing
end

function compute_auxiliary_thermodynamic_variables!(model::AtmosphereModel)
    grid = model.grid
    arch = grid.architecture

    launch!(arch, grid, :xyz,
            _compute_auxiliary_thermodynamic_variables!,
            model.temperature,
            model.specific_moisture,
            model.formulation,
            grid,
            model.thermodynamic_constants,
            model.microphysics,
            model.microphysical_fields,
            model.moisture_density)

    fill_halo_regions!(model.temperature)
    fill_halo_regions!(model.specific_moisture)
    fill_halo_regions!(model.microphysical_fields)
    fill_halo_regions!(model.formulation.thermodynamics)

    return nothing
end

@kernel function _compute_velocities!(velocities, grid, formulation, momentum)
    i, j, k = @index(Global, NTuple)

    œÅ = formulation_density(formulation)

    @inbounds begin
        œÅu = momentum.œÅu[i, j, k]
        œÅv = momentum.œÅv[i, j, k]
        œÅw = momentum.œÅw[i, j, k]

        œÅ·∂ú = œÅ[i, j, k]
        œÅ·∂† = ‚Ñëz·µÉ·µÉ·∂†(i, j, k, grid, œÅ)

        velocities.u[i, j, k] = œÅu / œÅ·∂ú
        velocities.v[i, j, k] = œÅv / œÅ·∂ú
        velocities.w[i, j, k] = œÅw / œÅ·∂†
    end
end

@kernel function _compute_auxiliary_thermodynamic_variables!(temperature,
                                                             specific_moisture,
                                                             formulation,
                                                             grid,
                                                             constants,
                                                             microphysics,
                                                             microphysical_fields,
                                                             moisture_density)
    i, j, k = @index(Global, NTuple)

    compute_auxiliary_thermodynamic_variables!(formulation, i, j, k, grid)

    œÅ_field = formulation_density(formulation)
    @inbounds begin
        œÅ = œÅ_field[i, j, k]
        œÅq·µó = moisture_density[i, j, k]
        q·µó = œÅq·µó / œÅ
        specific_moisture[i, j, k] = q·µó
    end

    ùí∞‚ÇÄ = diagnose_thermodynamic_state(i, j, k, grid,
                                      formulation,
                                      microphysics,
                                      microphysical_fields,
                                      constants,
                                      specific_moisture)

    # Adjust the thermodynamic state if using a microphysics scheme
    # that invokes saturation adjustment
    ùí∞‚ÇÅ = maybe_adjust_thermodynamic_state(i, j, k, ùí∞‚ÇÄ, microphysics, œÅ, microphysical_fields, q·µó, constants)

    update_microphysical_fields!(microphysical_fields, microphysics,
                                 i, j, k, grid,
                                 œÅ, ùí∞‚ÇÅ, constants)

    T = Thermodynamics.temperature(ùí∞‚ÇÅ, constants)
    @inbounds temperature[i, j, k] = T
end

@kernel function _compute_potential_temperature_auxiliary_variables!(temperature,
                                                                     potential_temperature,
                                                                     specific_moisture,
                                                                     grid,
                                                                     constants,
                                                                     formulation,
                                                                     microphysics,
                                                                     microphysical_fields,
                                                                     liquid_ice_potential_temperature_density,
                                                                     moisture_density)
    i, j, k = @index(Global, NTuple)

    œÅ_field = formulation_density(formulation)
    @inbounds begin
        œÅŒ∏ = liquid_ice_potential_temperature_density[i, j, k]
        œÅq·µó = moisture_density[i, j, k]
        œÅ = œÅ_field[i, j, k]

        Œ∏ = œÅŒ∏ / œÅ
        q·µó = œÅq·µó / œÅ
        potential_temperature[i, j, k] = Œ∏
        specific_moisture[i, j, k] = q·µó
    end

    ùí∞‚ÇÄ = diagnose_thermodynamic_state(i, j, k, grid,
                                      formulation,
                                      microphysics,
                                      microphysical_fields,
                                      constants,
                                      specific_moisture)

    # Adjust the thermodynamic state if using a microphysics scheme
    # that invokes saturation adjustment
    ùí∞‚ÇÅ = maybe_adjust_thermodynamic_state(i, j, k, ùí∞‚ÇÄ, microphysics, œÅ, microphysical_fields, q·µó, constants)

    update_microphysical_fields!(microphysical_fields, microphysics,
                                 i, j, k, grid,
                                 œÅ, ùí∞‚ÇÅ, constants)

    T = Thermodynamics.temperature(ùí∞‚ÇÅ, constants)
    @inbounds temperature[i, j, k] = T
end

function compute_tendencies!(model::AnelasticModel)
    grid = model.grid
    arch = grid.architecture
    GœÅu = model.timestepper.G‚Åø.œÅu
    GœÅv = model.timestepper.G‚Åø.œÅv
    GœÅw = model.timestepper.G‚Åø.œÅw

    model_fields = fields(model)

    #####
    ##### Momentum tendencies
    #####

    momentum_args = (
        formulation_density(model.formulation),
        model.advection.momentum,
        model.velocities,
        model.closure,
        model.closure_fields,
        model.momentum,
        model.coriolis,
        model.clock,
        model_fields)

    u_args = tuple(momentum_args..., model.forcing.œÅu)
    v_args = tuple(momentum_args..., model.forcing.œÅv)

    # Extra arguments for vertical velocity are required to compute
    # buoyancy:
    w_args = tuple(momentum_args..., model.forcing.œÅw,
                   model.formulation,
                   model.temperature,
                   model.specific_moisture,
                   model.microphysics,
                   model.microphysical_fields,
                   model.thermodynamic_constants)

    launch!(arch, grid, :xyz, compute_x_momentum_tendency!, GœÅu, grid, u_args)
    launch!(arch, grid, :xyz, compute_y_momentum_tendency!, GœÅv, grid, v_args)
    launch!(arch, grid, :xyz, compute_z_momentum_tendency!, GœÅw, grid, w_args)

    # Arguments common to energy density, moisture density, and tracer density tendencies:
    common_args = (
        model.formulation,
        model.thermodynamic_constants,
        model.specific_moisture,
        model.velocities,
        model.microphysics,
        model.microphysical_fields,
        model.closure,
        model.closure_fields,
        model.clock,
        model_fields)

    #####
    ##### Thermodynamic density tendency (dispatches on thermodynamics type)
    #####

    compute_thermodynamic_tendency!(model, common_args)

    #####
    ##### Moisture density tendency
    #####

    œÅq_args = (
        model.specific_moisture,
        Val(2),
        Val(:œÅq·µó),
        model.forcing.œÅq·µó,
        model.advection.œÅq·µó,
        common_args...)

    GœÅq·µó = model.timestepper.G‚Åø.œÅq·µó
    launch!(arch, grid, :xyz, compute_scalar_tendency!, GœÅq·µó, grid, œÅq_args)

    #####
    ##### Tracer density tendencies
    #####

    prognostic_microphysical_fields = NamedTuple(name => model.microphysical_fields[name]
                                                 for name in prognostic_field_names(model.microphysics))

    scalars = merge(prognostic_microphysical_fields, model.tracers)
    for (i, name) in enumerate(keys(scalars))
        œÅc = scalars[name]

        scalar_args = (
            œÅc,
            Val(i + 2),
            Val(name),
            model.forcing[name],
            model.advection[name],
            common_args...)

        GœÅc = getproperty(model.timestepper.G‚Åø, name)
        launch!(arch, grid, :xyz, compute_scalar_tendency!, GœÅc, grid, scalar_args)
    end

    return nothing
end

# See dynamics_kernel_functions.jl
@kernel function compute_scalar_tendency!(Gc, grid, args)
    i, j, k = @index(Global, NTuple)
    @inbounds Gc[i, j, k] = scalar_tendency(i, j, k, grid, args...)
end

@kernel function compute_static_energy_tendency!(GœÅe, grid, args)
    i, j, k = @index(Global, NTuple)
    @inbounds GœÅe[i, j, k] = static_energy_tendency(i, j, k, grid, args...)
end

@kernel function compute_potential_temperature_tendency!(GœÅŒ∏, grid, args)
    i, j, k = @index(Global, NTuple)
    @inbounds GœÅŒ∏[i, j, k] = potential_temperature_tendency(i, j, k, grid, args...)
end

@kernel function compute_x_momentum_tendency!(GœÅu, grid, args)
    i, j, k = @index(Global, NTuple)
    @inbounds GœÅu[i, j, k] = x_momentum_tendency(i, j, k, grid, args...)
end

@kernel function compute_y_momentum_tendency!(GœÅv, grid, args)
    i, j, k = @index(Global, NTuple)
    @inbounds GœÅv[i, j, k] = y_momentum_tendency(i, j, k, grid, args...)
end

@kernel function compute_z_momentum_tendency!(GœÅw, grid, args)
    i, j, k = @index(Global, NTuple)
    @inbounds GœÅw[i, j, k] = z_momentum_tendency(i, j, k, grid, args...)
end

"""
$(TYPEDSIGNATURES)

Apply boundary conditions by adding flux divergences to the right-hand-side.
"""
function TimeSteppers.compute_flux_bc_tendencies!(model::AtmosphereModel)

    G‚Åø = model.timestepper.G‚Åø
    arch  = model.architecture

    # Compute boundary flux contributions
    prognostic_model_fields = prognostic_fields(model)
    args = (arch, model.clock, fields(model))
    field_indices = 1:length(prognostic_model_fields)
    G‚Åø = model.timestepper.G‚Åø

    # TODO: should we call tracer_density_to_specific!(model) here?
    foreach(q -> compute_x_bcs!(G‚Åø[q], prognostic_model_fields[q], args...), field_indices)
    foreach(q -> compute_y_bcs!(G‚Åø[q], prognostic_model_fields[q], args...), field_indices)
    foreach(q -> compute_z_bcs!(G‚Åø[q], prognostic_model_fields[q], args...), field_indices)

    return nothing
end
