# [Atmosphere Thermodynamics](@id Thermodynamics-section)

```@setup thermo
using Breeze
thermo = ThermodynamicConstants()
```

Breeze implements thermodynamic relations for moist atmospheres.
By "moist", we mean that the atmosphere is a mixture of four components:
two gas phases _(i)_ "dry" air and _(ii)_ "vapor", and two "condensed" phases _(iii)_ "liquid", and _(iv)_ "ice".
Moisture makes _life_ interesting, because vapor can _condense_ or _solidify_
(and liquid can _freeze_) into liquid droplets and ice particles - with major consequences.

On Earth, dry air is itself a mixture of gases with fixed composition
constant [molar mass](https://en.wikipedia.org/wiki/Molar_mass).
Dry air on Earth's is mostly nitrogen, oxygen, and argon, whose combination produces the typical
(and Breeze's default) dry air molar mass

```@example thermo
using Breeze
thermo = ThermodynamicConstants()
thermo.dry_air.molar_mass
```

The vapor, liquid, and ice components are ``\mathrm{H_2 O}``, also known as "water".
Water vapor, which in Breeze has the default molar mass

```@example thermo
thermo.vapor.molar_mass
```

is lighter than dry air. As a result, moist, humid air is _lighter_ than dry air.

Liquid in Earth's atmosphere consists of falling droplets that range from tiny, nearly-suspended mist particles
to careening fat rain drops.
Ice in Earth's atmosphere consists of crystals, graupel, sleet, hail and, snow.

## "Moist" thermodynamic relations for a four-component mixture

What does it mean that moist air is a mixture of four components?
It means that the total mass ``M`` of air per volume, or _density_, ``œÅ``,
can be expressed as the sum of the masses of the individual components over the total volume ``V``,

```math
œÅ = \frac{M}{V} = \frac{m·µà + m·µõ + mÀ° + m‚Å±}{V} = œÅ·µà + œÅ·µõ + œÅÀ° + œÅ‚Å±
```

where ``m·µà``, ``m·µõ``, ``mÀ°``, and ``m‚Å±`` are the masses of dry air, vapor, liquid, and ice, respectively, while ``œÅ·µà``, ``œÅ·µõ``, ``œÅÀ°``, and ``œÅ‚Å±`` denote their _fractional densities_, which are the specific densities of the air components weighted by their respective _mass fractions_

```math
q·µà ‚â° \frac{m·µà}{M} = \frac{œÅ·µà}{œÅ} , \qquad q·µõ ‚â° \frac{m·µõ}{M} = \frac{œÅ·µõ}{œÅ} , \qquad
qÀ° ‚â° \frac{mÀ°}{M} = \frac{œÅÀ°}{œÅ}, \qquad \text{and} \qquad q‚Å± ‚â° \frac{œÅ‚Å±}{œÅ} = \frac{m‚Å±}{M} .
```

!!! note "The significance of certain superscripts"
    Throughout this documentation, superscripts are used to distinguish the components of moist air:
    - ``d`` denotes "dry air"
    - ``v`` denotes "vapor"
    - ``l`` denotes "liquid"
    - ``i`` denotes "ice"

    A fifth super script ``t`` is used to denote "total".
    For example, ``q·µà`` is the mass fraction of dry air, ``q·µõ`` is the mass fraction of vapor,
    ``qÀ°`` is the mass fraction of liquid, ``q‚Å±`` is the mass fraction of ice,
    and

    ```math
    q·µó = q·µõ + qÀ° + q‚Å±
    ```

    is the "total" mass fraction of the moisture components.

The liquid and ice components are not always present.
For example, a model with warm-phase microphysics does not have ice.
With no microphysics at all, there is no liquid _or_ ice.

By definition, all of the mass fractions sum up to unity,

```math
1 = q·µà + q·µõ + qÀ° + q‚Å± ,
```

so that, using ``q·µó = q·µõ + qÀ° + q‚Å±``, the dry air mass fraction can be diagnosed with ``q·µà = 1 - q·µó``.
The sometimes tedious bookkeeping required to correctly diagnose the effective mixture properties
of moist air are facilitated by Breeze's handy `MoistureMassFractions` abstraction.
For example,

```@example thermo
q = Breeze.Thermodynamics.MoistureMassFractions(0.01, 0.002, 1e-5)
```

from which we can compute the total moisture mass fraction,

```@example thermo
q·µó = Breeze.Thermodynamics.total_moisture_mass_fraction(q)
```

And the dry as well,

```@example thermo
q·µà = Breeze.Thermodynamics.dry_air_mass_fraction(q)
```

To be sure,

```@example thermo
q·µà + q·µó
```

## Two laws for ideal gases

Both dry air and vapor are modeled as ideal gases, which means that
the [ideal gas law](https://en.wikipedia.org/wiki/Ideal_gas_law) relates
pressure ``p``, temperature ``T``, and density ``œÅ``,

```math
p = œÅ R T .
```

Above, ``R ‚â° ‚Ñõ / m`` is the specific gas constant given the
[molar or "universal" gas constant](https://en.wikipedia.org/wiki/Gas_constant)
``‚Ñõ ‚âà 8.31 \; \mathrm{J} \, \mathrm{K}^{-1} \, \mathrm{mol}^{-1}`` and molar mass ``m`` of the gas species under consideration.

The [first law of thermodynamics](https://en.wikipedia.org/wiki/First_law_of_thermodynamics),
aka "conservation of energy", states that infinitesimal changes in
"heat content"[^1] ``\mathrm{d} \mathcal{H}`` are related to infinitesimal changes
in temperature ``\mathrm{d} T`` and pressure ``\mathrm{d} p`` according to:[^2]

```math
\mathrm{d} \mathcal{H} = c·µñ \mathrm{d} T - \frac{\mathrm{d} p}{œÅ} ,
```

[^1]: ``\mathcal{H}`` is called [_enthalpy_](https://en.wikipedia.org/wiki/Enthalpy)

[^2]: The conservation of energy states that any external heat input into the gas must equal the sum
      of the change of the gas's internal energy and the work done by the gas, ``p \, \mathrm{d} V``.
      For atmospheric flows it's convenient to express everything per unit mass. Assuming the mass of
      the fluid is conserved, we have that the work done per unit mass is ``p \, \mathrm{d}(\rho^{-1})``
      and the internal energy per unit mass is ``\mathcal{I} ‚â° c·µõ \mathrm{d} T``.
      Therefore, if ``\mathrm{d} \mathcal{H}`` is the change in heat content per unit mass,
      we have:

    ```math
    \mathrm{d} \mathcal{H} = c·µõ \mathrm{d} T + p \, \mathrm{d}(œÅ^{-1}) .
    ```

    By utilizing the identity ``\mathrm{d}(p / œÅ) = p \, \mathrm{d}(œÅ^{-1}) + œÅ^{-1} \mathrm{d}p`` and using
    the ideal gas, we can rewrite the above conservation law as:

    ```math
    \mathrm{d} \mathcal{H} = (c·µõ + R) \mathrm{d} T - œÅ^{-1} \mathrm{d}p ,
    ```

    which is the expression in the main text after noting that the specific heat capacities under
    constant pressure and under constant volume are related via ``c·µñ ‚â° c·µõ + R``.


where ``c·µñ`` is the specific heat capacity at constant pressure of the gas in question.

For example, to represent dry air typical for Earth, with molar mass ``m = 0.029 \; \mathrm{kg} \, \mathrm{mol}^{-1}`` and constant-pressure heat capacity ``c^p = 1005 \; \mathrm{J} \, \mathrm{kg}^{-1} \, \mathrm{K}^{-1}``,
we write

```@example thermo
using Breeze.Thermodynamics: IdealGas
dry_air = IdealGas(molar_mass=0.029, heat_capacity=1005)
```

We can also change the properties of dry air by specifying new values
when constructing `ThermodynamicConstants`,

```@example thermo
weird_thermo = ThermodynamicConstants(dry_air_molar_mass=0.042, dry_air_heat_capacity=420)
weird_thermo.dry_air
```

### Potential temperature and "adiabatic" transformations

Within adiabatic transformations, ``\mathrm{d} \mathcal{H} = 0``.
Then, combining the ideal gas law with conservation of energy yields

```math
\frac{\mathrm{d} T}{T} = \frac{R}{c·µñ} \frac{\mathrm{d} p}{p} ,
```

which implies that ``T ‚àº ( p / p‚ÇÄ )^{R / c·µñ}``,
where ``p‚ÇÄ`` is some reference pressure value.

As a result, the _potential temperature_, ``Œ∏``, defined as

```math
Œ∏ ‚â° T \big / \left ( \frac{p}{p‚ÇÄ} \right )^{R / c·µñ} = \frac{T}{Œ†} ,
```

remains constant under adiabatic transformations.
Notice that above, we also defined the Exner function, ``Œ† ‚â° ( p / p‚ÇÄ )^{R / c·µñ}``.

!!! note "About subscripts"
    The subscript "0" typically indicates some quantity evaluated at the surface ``z=0``.
    By convention, we tend to invoke constants that represent profiles evaluated at ``z=0``: i.e., ``p‚ÇÄ = p(z=0)``, ``T‚ÇÄ = T(z=0)``, etc.
    This implies that the potential temperature under adiabatic transformation is ``Œ∏(z) = Œ∏‚ÇÄ = T‚ÇÄ``.

### Hydrostatic balance

Next we consider a reference state that does not exchange energy with its environment
(i.e., ``\mathrm{d} \mathcal{H} = 0``) and thus has constant potential temperature

```math
Œ∏‚ÇÄ = T·µ£ \left ( \frac{p‚ÇÄ}{p·µ£} \right )^{R / c·µñ} .
```

!!! note "Reference states"
    Subscripts ``r`` indicate a _reference_ state.
    The adiabatic, hydrostatically-balanced reference state in the process
    of elucidation presently has a ``z`` dependent reference pressure ``p·µ£(z)``,
    density ``œÅ·µ£(z)``, and temperature ``T·µ£(z)``.
    This reference state also has a _constant_ potential temperature
    ``Œ∏‚ÇÄ``, which we attempt to clarify by writing ``Œ∏‚ÇÄ`` (since it's constant,
    it has the same value at ``z=0`` as at any height).
    We apologize that our notation differs from the usual in which
    ``0`` subscripts indicate "reference" (ü§î) and ``00`` (ü´£) means ``z=0``.

Hydrostatic balance requires

```math
‚àÇ_z p·µ£ = - œÅ·µ£ g ,
```

where ``g`` is gravitational acceleration, naturally by default

```@example thermo
thermo.gravitational_acceleration
```

By combining the hydrostatic balance with the ideal gas law and the definition of potential
temperature we get

```math
\frac{p·µ£}{p‚ÇÄ} = \left (1 - \frac{g z}{c·µñ Œ∏‚ÇÄ} \right )^{c·µñ / R} .
```

Thus

```math
\begin{align*}
T·µ£(z) & = Œ∏‚ÇÄ \left ( \frac{p·µ£}{p‚ÇÄ} \right )^{R / c·µñ} \\
      & = Œ∏‚ÇÄ - \frac{g}{c·µñ} z,
\end{align*}
```

and

```math
œÅ·µ£(z) = \frac{p‚ÇÄ}{R·µà Œ∏‚ÇÄ} \left ( 1 - \frac{g z}{c·µñ Œ∏‚ÇÄ} \right )^{c·µñ / R - 1} .
```

The quantity ``g / c·µñ ‚âà 9.76 \;\mathrm{K}\,\mathrm{km}^{-1}`` that appears above is also referred to as
the "[dry adiabatic lapse rate](https://en.wikipedia.org/wiki/Lapse_rate)".

### An example of a dry reference state in Breeze

We can visualise a hydrostatic reference profile evaluating Breeze's reference-state
utilities (which assume a dry reference state) on a one-dimensional `RectilinearGrid`.
In the following code, the superscript ``d`` denotes dry air, e.g., an ideal gas
with ``R·µà = 286.71 \; \mathrm{J} \, \mathrm{K}^{-1}``:

```@example reference_state
using Breeze
using CairoMakie

grid = RectilinearGrid(size=160, z=(0, 12_000), topology=(Flat, Flat, Bounded))
thermo = ThermodynamicConstants()
reference_state = ReferenceState(grid, thermo, base_pressure=101325, potential_temperature=288)

p·µ£ = reference_state.pressure
œÅ·µ£ = reference_state.density

R·µà = Breeze.Thermodynamics.dry_air_gas_constant(thermo)
c·µñ·µà = thermo.dry_air.heat_capacity
p‚ÇÄ = reference_state.base_pressure
Œ∏‚ÇÄ = reference_state.potential_temperature
g = thermo.gravitational_acceleration

# Verify that T·µ£ = Œ∏‚ÇÄ - (g / c·µñ·µà) * z
z = KernelFunctionOperation{Center, Center, Center}(znode, grid, Center(), Center(), Center())
T·µ£‚ÇÅ = Field(Œ∏‚ÇÄ * (p·µ£ / p‚ÇÄ)^(R·µà / c·µñ·µà))
T·µ£‚ÇÇ = Field(Œ∏‚ÇÄ - (g / c·µñ·µà) * z)

fig = Figure()

axT = Axis(fig[1, 1]; xlabel = "Temperature (·µíK)", ylabel = "Height (m)")
lines!(axT, T·µ£‚ÇÅ)
lines!(axT, T·µ£‚ÇÇ, linestyle = :dash, color = :orange, linewidth = 2)

axp = Axis(fig[1, 2]; xlabel = "Pressure (10‚Åµ Pa)", yticklabelsvisible = false)
lines!(axp, p·µ£ / 1e5)

axœÅ = Axis(fig[1, 3]; xlabel = "Density (kg m‚Åª¬≥)", yticklabelsvisible = false)
lines!(axœÅ, œÅ·µ£)

fig
```

## The gaseous nature of moist air

To define the gaseous nature of moist air - that is, the equation of state relating density and pressure,
we assume that the _volume_ of liquid and ice components are negligible.
As a result, moist air pressure is the sum of partial pressures of vapor and dry air with no
contribution from liquid or ice phases,

```math
p = p·µà + p·µõ .
```

Because the dry air and vapor components are ideal gases, their densities are related to pressure through the
ideal gas law,

```math
p·µà = œÅ·µà R·µà T \qquad \text{and} \qquad p·µõ = œÅ·µõ R·µõ T ,
```

where ``T`` is temperature, ``R‚Å± = ‚Ñõ / m^Œ≤`` is the specific gas constant for component ``Œ≤``,
``m^Œ≤`` is the molar mass of component ``Œ≤``, and
``‚Ñõ``  is the [molar or "universal" gas constant](https://en.wikipedia.org/wiki/Gas_constant),

```@example thermo
thermo = ThermodynamicConstants()
thermo.molar_gas_constant
```

`ThermodynamicConstants`, which is central to Breeze's implementation of moist thermodynamics.
holds constants like the molar gas constant and molar masses, latent heats, gravitational acceleration, and more,

```@example thermo
thermo
```

These default values evince basic facts about water vapor air typical to Earth's atmosphere:
for example, the molar masses of dry air (itself a mixture of mostly nitrogen, oxygen, and argon),
and water vapor are ``m·µà = 0.029 \; \mathrm{kg} \, \mathrm{mol}^{-1}`` and ``m·µõ = 0.018 \; \mathrm{kg} \, \mathrm{mol}^{-1}``.
And even more interesting, the triple point temperature and pressure of water vapor are

```@example thermo
thermo.triple_point_temperature, thermo.triple_point_pressure
```

not so far from the typical conditions we experience on Earth's surface - one of the reasons that things are
so interesting down here. Also, that temperature is not a typo: the triple point temperature really is just
``+0.01^\circ``C.


It's then convenient to introduce the "mixture" gas constant ``R·µê(q·µõ)`` such that

```math
p = œÅ R·µê T, \qquad \text{where} \qquad R·µê ‚â° q·µà R·µà + q·µõ R·µõ .
```

To illustrate, let's compute the mixture gas constant ``R·µê`` for air with a small amount of water vapor.
The contribution of vapor increases ``R·µê`` above the dry air value:

```@example thermo
q = Breeze.Thermodynamics.MoistureMassFractions(0.01, 0.0, 0.0) # 1% vapor by mass
R·µà = Breeze.Thermodynamics.dry_air_gas_constant(thermo)
R·µê = Breeze.Thermodynamics.mixture_gas_constant(q, thermo)
R·µê - R·µà # shows the uplift from the vapor component
```

A small increase in specific humidity increases the effective gas constant of air.

## The thermal properties of moist air

Though we neglect the volume of liquid and ice, we do not neglect their _mass_ or _energy_.
The heat capacity of moist air thus includes contributions from all four components,

```math
c·µñ·µê = q·µà c·µñ·µà + q·µõ c·µñ·µõ + qÀ° cÀ° + q‚Å± c‚Å±,
```

where the ``c·µñ·µù`` denote the specific heat capacity at constant pressure of
constituent ``Œ≤``, and we have neglected the superscript ``p`` for liquid
and ice because they are assumed incompressible (their specific heats and constant
pressure or volume are the same).
We call ``c·µñ·µê`` the "mixture heat capacity", and because with default parameters the
heat capacity of dry air is the smallest of either vapor, liquid, or ice,
any moisture at all tends to increase the mixture heat capacity,

```@example thermo
q = Breeze.Thermodynamics.MoistureMassFractions(0.01, 0.0, 0.0)
c·µñ·µà = thermo.dry_air.heat_capacity
c·µñ·µê = Breeze.Thermodynamics.mixture_heat_capacity(q, thermo)
c·µñ·µê - c·µñ·µà
```


## The Clausius--Clapeyron relation and saturation specific humidity

The [Clausius--Clapeyron relation](https://en.wikipedia.org/wiki/Clausius%E2%80%93Clapeyron_relation)
for an ideal gas

```math
\frac{\mathrm{d} p·µõ‚Å∫}{\mathrm{d} T} = \frac{p·µõ‚Å∫ ‚Ñí^Œ≤(T)}{R·µõ T^2} ,
```

where ``p·µõ‚Å∫`` is saturation vapor pressure, ``T`` is temperature, ``R·µõ`` is the specific
gas constant for vapor, ``‚Ñí^Œ≤(T)`` is the latent heat of the transition from vapor to the
``Œ≤`` phase (e.g., ``Œ≤ = l`` for vapor ‚Üí liquid and ``Œ≤ = i`` for vapor to ice).

For a thermodynamic formulation that uses constant (i.e. temperature-independent) specific
heats, the latent heat of a phase transition is linear in temperature.
For example, for phase change from vapor to liquid,

```math
‚ÑíÀ°(T) = ‚ÑíÀ°(T=0) + \big ( \underbrace{c·µñ·µõ - cÀ°}_{‚â°ŒîcÀ°} \big ) T ,
```

where ``‚ÑíÀ°(T=0)`` is the latent heat at absolute zero, ``T = 0 \; \mathrm{K}``.
By integrating from the triple-point temperature ``T·µó ≥`` for which ``p(T·µó ≥) = p·µó ≥``, we get

```math
p·µõ‚Å∫(T) = p·µó ≥ \left ( \frac{T}{T·µó ≥} \right )^{ŒîcÀ° / R·µõ} \exp \left [ \frac{‚ÑíÀ°(T=0)}{R·µõ} \left (\frac{1}{T·µó ≥} - \frac{1}{T} \right ) \right ] .
```

Consider parameters for liquid water,

```@example thermo
using Breeze.Thermodynamics: CondensedPhase
liquid_water = CondensedPhase(reference_latent_heat=2500800, heat_capacity=4181)
```

or water ice,

```@example thermo
water_ice = CondensedPhase(reference_latent_heat=2834000, heat_capacity=2108)
```

The saturation vapor pressure is

```@example
using Breeze
using Breeze.Thermodynamics: saturation_vapor_pressure

thermo = ThermodynamicConstants()

T = collect(200:0.1:320)
p·µõÀ°‚Å∫ = [saturation_vapor_pressure(T‚Å±, thermo, thermo.liquid) for T‚Å± in T]
p·µõ‚Å±‚Å∫ = [saturation_vapor_pressure(T‚Å±, thermo, thermo.ice) for T‚Å± in T]
p·µõ‚Å±‚Å∫[T .> thermo.triple_point_temperature] .= NaN

using CairoMakie

fig = Figure()
ax = Axis(fig[1, 1], xlabel="Temperature (·µíK)", ylabel="Saturation vapor pressure p·µõ‚Å∫ (Pa)", yscale = log10, xticks=200:20:320)
lines!(ax, T, p·µõÀ°‚Å∫, label="vapor pressure over liquid")
lines!(ax, T, p·µõ‚Å±‚Å∫, linestyle=:dash, label="vapor pressure over ice")
axislegend(ax, position=:rb)
fig
```

The saturation specific humidity is

```math
q·µõ‚Å∫ ‚â° \frac{œÅ·µõ‚Å∫}{œÅ} = \frac{p·µõ‚Å∫}{œÅ R·µê T} .
```

and this is what it looks like:

```@example
using Breeze
using Breeze.Thermodynamics: saturation_specific_humidity

thermo = ThermodynamicConstants()

p‚ÇÄ = 101325
R·µà = Breeze.Thermodynamics.dry_air_gas_constant(thermo)
T = collect(273.2:0.1:313.2)
q·µõ‚Å∫ = zeros(length(T))

for i = 1:length(T)
    œÅ = p‚ÇÄ / (R·µà * T[i])
    q·µõ‚Å∫[i] = saturation_specific_humidity(T[i], œÅ, thermo, thermo.liquid)
end

using CairoMakie

fig = Figure()
ax = Axis(fig[1, 1], xlabel="Temperature (·µíK)", ylabel="Saturation specific humidity q·µõ‚Å∫ (kg kg‚Åª¬π)")
lines!(ax, T, q·µõ‚Å∫)
fig
```

## Moist static energy

For moist air, a convenient thermodynamic invariant that couples temperature, composition, and height is the moist static energy (MSE),

```math
e ‚â° c·µñ·µê \, T + g z - LÀ°·µ£ \, q·µõ - L‚Å±·µ£ q‚Å± .
```

!!! note "The alternative 'frozen moist static energy' variable"

    An alternative, physically equivalent, definition of moist static energy used in atmospheric
    models such as the Global System for Atmospheric Modeling (GSAM) [Maxwell2020](@cite) is

    ```math
    eÃÉ ‚â° c·µñ·µê \, T + g z + LÀ°·µ£ \, q·µó - L·∂†·µ£ q‚Å± .
    ```

    ``e`` and ``eÃÉ`` are not the same, but they obey the same conservation equation provided
    that total moisture fraction is conserved, or that ``\mathrm{D}q·µó / \mathrm{D}t = 0``.

## Liquid-ice potential temperature
